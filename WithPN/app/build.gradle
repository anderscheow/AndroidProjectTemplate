apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    flavorDimensions "env"
    productFlavors {
        dev {
            dimension "env"
            applicationId 'com.easipos.template.dev'
            // Mmmppbbb (M - major, m - minor, p - patch, b - build)
            versionCode 10000000
            versionName '1.0.0-dev-build0'
            buildConfigField 'String', 'API_DOMAIN', '"http://www.example.com/api/"'
            manifestPlaceholders = [
                    onesignal_app_id               : '9501cf5b-7037-4b78-ae96-0b10524fbc0e',
                    // Project number pulled from dashboard, local value is ignored.
                    onesignal_google_project_number: 'REMOTE'
            ]
        }
        prod {
            dimension "env"
            applicationId 'com.easipos.template'
            // Mmmppbbb (M - major, m - minor, p - patch, b - build)
            versionCode 10000000
            versionName '1.0.0-build0'
            buildConfigField 'String', 'API_DOMAIN', '"https://www.example.com/api/"'
            manifestPlaceholders = [
                    onesignal_app_id               : '9501cf5b-7037-4b78-ae96-0b10524fbc0e',
                    // Project number pulled from dashboard, local value is ignored.
                    onesignal_google_project_number: 'REMOTE'
            ]
        }
    }
    signingConfigs {
        release {
            keyAlias "secret"
            keyPassword "easipos1qazZAQ!"
            storeFile file("release.keystore")
            storePassword "easipos1qazZAQ!"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            firebaseCrashlytics {
                // If you don't need crash reporting for your debug build,
                // you can speed up your build by disabling mapping file uploading.
                mappingFileUploadEnabled false
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.all {
                    def newName = "ERM_${variant.productFlavors[0].name}_${versionCode}_release.apk"
                    outputFileName = newName
                }
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/*'
    }
    dataBinding {
        enabled = true
    }
    androidExtensions {
        experimental = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.room:room-runtime:2.2.5'
    implementation 'androidx.room:room-ktx:2.2.5'
    implementation 'com.google.firebase:firebase-analytics:18.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.3.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.pixplicity.easyprefs:library:1.9.0'
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    implementation 'com.tapadoo.android:alerter:6.1.0'
    implementation 'com.onesignal:OneSignal:[3.11.2, 3.99.99]'
    implementation('com.github.ihsanbal:LoggingInterceptor:3.0.0') {
        exclude group: 'org.json', module: 'json'
    }
    implementation 'com.github.anderscheow:Android-Library:3.3.1'
    implementation 'com.github.anderscheow:Validator:2.2.1'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    kapt 'androidx.room:room-compiler:2.2.5'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
}

//apply plugin: 'com.google.gms.google-services'